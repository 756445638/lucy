
/*
	a simple but yet powerful cmd parse for lucy programing
*/
public final class  FlagParser {
	options map{ string -> Option };
	
	public fn FlagParser() {
	    this.super()
		this.options = new map{string -> Option}()
	}
	
	
	private fn checkOptionExist(name string) {
		if this.options.keyExist(name)  {
			panic(new error(sprintf("option already '%s' exists" , name )))
		}
	}
	
	public fn boolOption(option string , d bool ,usage string ) ->(o BoolOption){
		.checkOptionExist(option)
		if option.startsWith("-") == false {
			panic(new error(sprintf("'%s' not a valid option" , option )))
		}
		o = new BoolOption(d)
		o.typ = OptionTypeBool
		o.usage = usage
		this.options[option] = o
	} 
	
	public fn intOption(option string , d int ,usage string ) ->(o IntOption){
		.checkOptionExist(option)
		if option.startsWith("-") == false {
			panic(new error(sprintf("'%s' not a valid option" , option )))
		}
		o = new IntOption(d);
		o.typ = OptionTypeInt
		o.usage = usage 
		this.options[option] = o
	} 
	
	public fn stringOption(option string , d string ,usage string ) ->(o StringOption){
		.checkOptionExist(option)
		if option.startsWith("-") == false {
			panic(new error(sprintf("'%s' not a valid option" , option )))
		}
		o = new StringOption(d);
		o.typ = OptionTypeString
		o.usage = usage 
		this.options[option] = o
	}

	public fn floatOption(option string , d float ,usage string ) ->(o FloatOption){
		.checkOptionExist(option)
		if option.startsWith("-") == false {
			panic(new error(sprintf("'%s' not a valid option" , option )))
		}
		o = new FloatOption(d);
		o.typ = OptionTypeFloat
		o.usage = usage 
		this.options[option] = o
	} 
	
	
	
	public fn parse(args []string)->(noDefine []string , err error){
		for v := range this.options{
			v.reset()
		}
		noDefine = []string{} 
		print(args )
		for k := 0 ; k < len (args) ; k++{
			v := args[k]
			if false == this.options.keyExist(v) {
				noDefine.append(v)
				continue
			}
			o := this.options[v]
			if o.typ == OptionTypeBool {
				o.parse(v)
			}else {
				if k == len(args) -1 {
						err = new error(sprintf("no value  after '%s'" , v ))
						return 
				}
				err = o.parse(args[++k])
				if err != null {
					return 
				}
			}
			
		}
	}
	public fn helpMsg() -> (msg string){
		msg = "flag are:\n"
		for k,v := range this.options {
			msg += sprintf("\t '%s' default:'%s' usage:'%s'\n" , k , v.formatDefaultValue() , v.usage )
		}
	}

} 




















