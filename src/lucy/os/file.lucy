import "java/io";
import "lucy/lang";



public class File extends io.File {
	reader io.FileInputStream;
	writer io.FileOutputStream;
	
	public fn File(filepath string ,model string = ""){
		this.super(filepath);
		isCreate := false;
		isRead  := false;
		isWrite := false;
		for k,v := range []byte(model){
			if v == 'r' {
				isRead = true;
			}
			if v == 'w' {
				isWrite = true;
			}
			if v == 'c'{
				isCreate = true;
			}
		}
		if isCreate &&  this.exists() == false{
			if false == this.createNewFile(){
				panic(new error("create file faild"));
			}
		}
		if isRead {
			this.reader = new io.FileInputStream(this);
		}
		if isWrite {
			this.writer = new io.FileOutputStream(this);
		}
	}

	public fn read(bs []byte)->(n int){
		n = this.reader.read(bs.toJavaArray()); 
	}

	public fn write(bs []byte) {
		this.writer.write(bs.toJavaArray()); 
	}
	
	public fn close(){
		if this.reader != null {
			this.reader.close();
		}
		if this.writer != null {
			this.writer.close();
		}
	}
	public static fn readFile(filepath string)->(bs []byte,err error){
		file := new File(filepath);
		bs = new []byte(0);
		reader := new io.FileInputStream(file);
		defer reader.close();
		b := new []byte(1 << 10);
		var n int;
		for n != -1 {
			n = reader.read(b.toJavaArray());
			if n != -1 {
				bs.appendAll(b[0:n]);
			}
		}
	}
	
	public static fn writeFile( filepath string ,bs []byte){
		writer := new io.FileOutputStream(new io.File(filepath));
		defer writer.close();
		writer.write(bs.toJavaArray());
	}
	
	
	/*
	create directory
	*/
	public static fn mkDir(path string)->(ok bool){
		f := new io.File(path);
		ok = f.mkdirs();
	}

	/*
		delete a directory
		ok means if delete is ok
	*/
	public static fn removeDir(path string)->(ok bool){
		f := new io.File(path);
		ok = f.delete();
	}
	
	public static fn listDir(directory string)->(fis []File,err error){
		f := new File(directory);
		if false == f.isDirectory() && f.exists(){
			err = new error("file is not directory");
			return ;
		}
		if f.exists() == false {
			err = new error("file not exists");
			return ;
		}
		names := f.list();
		fis = new []File(names.size());
		for k,v := range names{
			fis[k] = new File(directory + "/" + v);
		}
		return fis,null;
	}
	
}