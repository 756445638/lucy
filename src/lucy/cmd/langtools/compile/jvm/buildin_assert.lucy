import "lucy/cmd/langtools/compile/ast"
import "lucy/cmd/langtools/compile/jvm/cg"

class BuildinFunctionAssert {
    
    eb BuildExpression
    
    fn BuildinFunctionAssert(eb BuildExpression){
        this.super()
        this.eb = eb
    }

    fn mkBuildInAssert(
        c cg.ClassHighLevel,
        code cg.AttributeCode,
        e ast.Expression,
        context Context,
        state StackMapState) -> (maxStack char) {
        call := e.Data.(ast.ExpressionFunctionCall)
        length := int(len(call.Args))
        lengthOffset := code.MaxLocals
        code.MaxLocals++

        state.appendLocals(c, typeInt)
        loadInt32(c, code, length)
        copyOPs(code, storeLocalVariableOps(ast.VariableTypeInt, lengthOffset))
        stepOffset := code.MaxLocals
        code.MaxLocals++
        state.appendLocals(c, typeInt)
        code.Codes[code.CodeLength] = cg.OP_iconst_0
        code.CodeLength++
        copyOPs(code, storeLocalVariableOps(ast.VariableTypeInt, stepOffset))
        exits := []cg.Exit{}
        for _, a := range call.Args {
            stack := this.eb.build(c, code, a, context, state)
            if stack > maxStack {
                maxStack = stack
            }
            exits.append(exits, new cg.Exit(cg.OP_ifeq, code))
            code.Codes[code.CodeLength] = cg.OP_iinc
            code.Codes[code.CodeLength+1] = byte(stepOffset)
            code.Codes[code.CodeLength+2] = 1
            code.CodeLength += 3
        }
        writeExits(exits, code.CodeLength)
        context.MakeStackMap(code, state, code.CodeLength)
        copyOPs(code, loadLocalVariableOps(ast.VariableTypeInt, lengthOffset)...)
        copyOPs(code, loadLocalVariableOps(ast.VariableTypeInt, stepOffset)...)
        okExit := (new cg.Exit(cg.OP_if_icmpeq, code))
        code.Codes[code.CodeLength] = cg.OP_ldc_w
        c.InsertStringConst("assert failed,expression->'%d'", code.Codes[code.CodeLength+1:code.CodeLength+3])
        code.CodeLength += 3
        loadInt32(c, code, 1)
        code.Codes[code.CodeLength] = cg.OP_anewarray
        c.InsertClassConst(javaRootClass, code.Codes[code.CodeLength+1:code.CodeLength+3])
        code.CodeLength += 3
        code.Codes[code.CodeLength] = cg.OP_dup
        code.CodeLength++
        code.Codes[code.CodeLength] = cg.OP_iconst_0
        code.CodeLength++
        copyOPs(code, loadLocalVariableOps(ast.VariableTypeInt, stepOffset)...)
        if 5 > maxStack {
            maxStack = 5
        }
        TypeConverterAndPrimitivePacker.packPrimitives(c, code,  typeInt)
        code.Codes[code.CodeLength] = cg.OP_aastore
        code.CodeLength++
        ast.TypeInsertMethodCall(code, cg.OP_invokestatic, javaStringClass,
            "format", "(Ljava/lang/String;[Ljava/lang/Object;)Ljava/lang/String;")

        code.Codes[code.CodeLength] = cg.OP_new
        c.InsertClassConst(javaExceptionClass, code.Codes[code.CodeLength+1:code.CodeLength+3])
        code.CodeLength += 3
        code.Codes[code.CodeLength] = cg.OP_dup_x1
        code.Codes[code.CodeLength+1] = cg.OP_swap
        code.CodeLength += 2
        ast.TypeInsertMethodCall(code,
            cg.OP_invokespecial,
            javaExceptionClass,
            specialMethodInit,
            "(Ljava/lang/String;)V")
        code.Codes[code.CodeLength] = cg.OP_athrow
        code.CodeLength++
        writeExits([]cg.Exit{okExit}, code.CodeLength)
        context.MakeStackMap(code, state, code.CodeLength)
        return
    }


}

