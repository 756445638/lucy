
import "lucy/cmd/langtools/compile/ast"
import "lucy/cmd/langtools/compile/jvm/cg"

class BuildExpressionConversion {
	
	eb BuildExpression
    
    fn BuildExpressionConversion(eb BuildExpression){
        this.super()
        this.eb = eb
    }
    
	fn  stackTop2Byte(code cg.AttributeCode, on ast.VariableTypeKind) {
		switch on {
		case ast.VariableTypeByte:
			// already is
		case ast.VariableTypeShort , ast.VariableTypeChar ,ast.VariableTypeInt :
			code.Codes[code.CodeLength] = cg.OP_i2b
			code.CodeLength++
		case ast.VariableTypeLong:
			code.Codes[code.CodeLength] = cg.OP_l2i
			code.Codes[code.CodeLength+1] = cg.OP_i2b
			code.CodeLength += 2
		case ast.VariableTypeFloat:
			code.Codes[code.CodeLength] = cg.OP_f2i
			code.Codes[code.CodeLength+1] = cg.OP_i2b
			code.CodeLength += 2
		case ast.VariableTypeDouble:
			code.Codes[code.CodeLength] = cg.OP_d2i
			code.Codes[code.CodeLength+1] = cg.OP_i2b
			code.CodeLength += 2
		}
	}

	fn  stackTop2Short(code cg.AttributeCode, on ast.VariableTypeKind) {
		switch on {
		case ast.VariableTypeByte:
			// already is
		case ast.VariableTypeShort:
			// already is
		case ast.VariableTypeChar:
			code.Codes[code.CodeLength] = cg.OP_i2s
			code.CodeLength++
		case ast.VariableTypeInt:
			code.Codes[code.CodeLength] = cg.OP_i2s
			code.CodeLength++
		case ast.VariableTypeLong:
			code.Codes[code.CodeLength] = cg.OP_l2i
			code.Codes[code.CodeLength+1] = cg.OP_i2s
			code.CodeLength += 2
		case ast.VariableTypeFloat:
			code.Codes[code.CodeLength] = cg.OP_f2i
			code.Codes[code.CodeLength+1] = cg.OP_i2s
			code.CodeLength += 2
		case ast.VariableTypeDouble:
			code.Codes[code.CodeLength] = cg.OP_d2i
			code.Codes[code.CodeLength+1] = cg.OP_i2s
			code.CodeLength += 2

		}
	}

	fn  stackTop2Char(code cg.AttributeCode, on ast.VariableTypeKind) {
		switch on {
		case ast.VariableTypeByte:
			// already is
		case ast.VariableTypeShort:
			// already is
		case ast.VariableTypeChar:
			// already is
		case ast.VariableTypeInt:
			code.Codes[code.CodeLength] = cg.OP_i2c
			code.CodeLength++
		case ast.VariableTypeLong:
			code.Codes[code.CodeLength] = cg.OP_l2i
			code.CodeLength++
			code.Codes[code.CodeLength] = cg.OP_i2c
			code.CodeLength++
		case ast.VariableTypeFloat:
			code.Codes[code.CodeLength] = cg.OP_f2i
			code.CodeLength++
			code.Codes[code.CodeLength] = cg.OP_i2c
			code.CodeLength++
		case ast.VariableTypeDouble:
			code.Codes[code.CodeLength] = cg.OP_d2i
			code.CodeLength++
			code.Codes[code.CodeLength] = cg.OP_i2c
			code.CodeLength++

		}
	}
	fn  stackTop2Int(code cg.AttributeCode, on ast.VariableTypeKind) {
		switch on {
		case ast.VariableTypeByte:
			// already is
		case ast.VariableTypeShort:
			// already is
		case ast.VariableTypeChar:
			// already is
		case ast.VariableTypeInt:
			// already is
		case ast.VariableTypeLong:
			code.Codes[code.CodeLength] = cg.OP_l2i
			code.CodeLength++
		case ast.VariableTypeFloat:
			code.Codes[code.CodeLength] = cg.OP_f2i
			code.CodeLength++
		case ast.VariableTypeDouble:
			code.Codes[code.CodeLength] = cg.OP_d2i
			code.CodeLength++

		}
	}

	fn  stackTop2Float(code cg.AttributeCode, on ast.VariableTypeKind) {
		switch on {
		case ast.VariableTypeByte ,ast.VariableTypeShort ,ast.VariableTypeChar ,ast.VariableTypeInt   :
			code.Codes[code.CodeLength] = cg.OP_i2f
			code.CodeLength++
		case ast.VariableTypeLong:
			code.Codes[code.CodeLength] = cg.OP_l2f
			code.CodeLength++
		case ast.VariableTypeFloat:
			// already is
		case ast.VariableTypeDouble:
			code.Codes[code.CodeLength] = cg.OP_d2f
			code.CodeLength++

		}
	}

	fn  stackTop2Long(code cg.AttributeCode, on ast.VariableTypeKind) {
		switch on {
		case ast.VariableTypeByte , ast.VariableTypeShort ,ast.VariableTypeChar ,ast.VariableTypeInt  :
			code.Codes[code.CodeLength] = cg.OP_i2l
			code.CodeLength++
		case ast.VariableTypeLong:
			// already is
		case ast.VariableTypeFloat:
			code.Codes[code.CodeLength] = cg.OP_f2l
			code.CodeLength++
		case ast.VariableTypeDouble:
			code.Codes[code.CodeLength] = cg.OP_d2l
			code.CodeLength++

		}
	}

	fn  stackTop2Double(code cg.AttributeCode, on ast.VariableTypeKind) {
		switch on {
		case ast.VariableTypeByte , ast.VariableTypeShort ,ast.VariableTypeChar , ast.VariableTypeInt :
			code.Codes[code.CodeLength] = cg.OP_i2d
			code.CodeLength++
		case ast.VariableTypeLong:
			code.Codes[code.CodeLength] = cg.OP_l2d
			code.CodeLength++
		case ast.VariableTypeFloat:
			code.Codes[code.CodeLength] = cg.OP_f2d
			code.CodeLength++
		case ast.VariableTypeDouble:
			// already is
		}
	}

	/*
		convert stack top to target
	*/
	fn  numberTypeConverter(code cg.AttributeCode,
		on ast.VariableTypeKind, target ast.VariableTypeKind) {
		if on == target {
			return
		}
		switch target {
		case ast.VariableTypeByte:
			this.stackTop2Byte(code, on)
		case ast.VariableTypeShort:
			this.stackTop2Short(code, on)
		case ast.VariableTypeChar:
			this.stackTop2Char(code, on)
		case ast.VariableTypeInt:
			this.stackTop2Int(code, on)
		case ast.VariableTypeLong:
			this.stackTop2Long(code, on)
		case ast.VariableTypeFloat:
			this.stackTop2Float(code, on)
		case ast.VariableTypeDouble:
			this.stackTop2Double(code, on)
		}
	}

	fn  stackTop2String(c cg.ClassHighLevel, code cg.AttributeCode,
		typ ast.Type, context Context, state StackMapState) -> (maxStack char) {
		if typ.Type == ast.VariableTypeString {
			return
		}
		maxStack = jvmSlotSize(typ) * 2
		switch typ.Type {
		case ast.VariableTypeBool:
			code.Codes[code.CodeLength] = cg.OP_invokestatic
			c.InsertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
				javaStringClass,
				"valueOf",
				"(Z)Ljava/lang/String;"
			), code.Codes[code.CodeLength+1:code.CodeLength+3])
			code.CodeLength += 3
		case ast.VariableTypeChar:
			code.Codes[code.CodeLength] = cg.OP_invokestatic
			c.InsertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
				javaStringClass,
				"valueOf",
				"(C)Ljava/lang/String;"
			), code.Codes[code.CodeLength+1:code.CodeLength+3])
			code.CodeLength += 3
		case ast.VariableTypeByte ,ast.VariableTypeShort ,  ast.VariableTypeEnum ,ast.VariableTypeInt :
			code.Codes[code.CodeLength] = cg.OP_invokestatic
			c.InsertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
				javaStringClass,
				"valueOf",
				"(I)Ljava/lang/String;"
			), code.Codes[code.CodeLength+1:code.CodeLength+3])
			code.CodeLength += 3
		case ast.VariableTypeLong:
			code.Codes[code.CodeLength] = cg.OP_invokestatic
			c.InsertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
				javaStringClass,
				"valueOf",
				"(J)Ljava/lang/String;"
			), code.Codes[code.CodeLength+1:code.CodeLength+3])
			code.CodeLength += 3
		case ast.VariableTypeFloat:
			code.Codes[code.CodeLength] = cg.OP_invokestatic
			c.InsertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
				javaStringClass,
				"valueOf",
				"(F)Ljava/lang/String;"
			), code.Codes[code.CodeLength+1:code.CodeLength+3])
			code.CodeLength += 3
		case ast.VariableTypeDouble:
			code.Codes[code.CodeLength] = cg.OP_invokestatic
			c.InsertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
				javaStringClass,
				"valueOf",
				"(D)Ljava/lang/String;"
			), code.Codes[code.CodeLength+1:code.CodeLength+3])
			code.CodeLength += 3
		default:
			code.Codes[code.CodeLength] = cg.OP_dup
			code.CodeLength++
			{
				state.pushStack(c, typ)
				context.MakeStackMap(code, state, code.CodeLength+10)
				state.popStack(1)
				state.pushStack(c, typeString)
				context.MakeStackMap(code, state, code.CodeLength+13)
				state.popStack(1)
			}
			code.Codes[code.CodeLength] = cg.OP_ifnonnull
			binary.BigEndian.PutUint16(code.Codes[code.CodeLength+1:code.CodeLength+3], 10)
			code.Codes[code.CodeLength+3] = cg.OP_pop
			code.Codes[code.CodeLength+4] = cg.OP_ldc_w
			c.InsertStringConst("null", code.Codes[code.CodeLength+5:code.CodeLength+7])
			code.Codes[code.CodeLength+7] = cg.OP_goto
			binary.BigEndian.PutUint16(code.Codes[code.CodeLength+8:code.CodeLength+10], 6)
			code.Codes[code.CodeLength+10] = cg.OP_invokevirtual
			c.InsertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
				"java/lang/Object",
				"toString",
				"()Ljava/lang/String;"
			), code.Codes[code.CodeLength+11:code.CodeLength+13])
			code.CodeLength += 13
		}
		return
	}
	
}


