import "lucy/cmd/langtools/compile/ast"


public class LucyMethodSignature {

	
	static fn need(ft ast.FunctionType) ->(n bool)  {
		for _, v := range ft.parameterList {
			if LucyFieldSignature.need(v.Type) {
				return true
			}
		}
		if ft.vArgs != null && LucyFieldSignature.need(ft.vArgs.Type) {
			return true
		}
		for _, v := range ft.returnList {
			if LucyFieldSignature.need(v.Type) {
				return true
			}
		}
		if len(ft.returnList) > 1 {
			return true
		}
		return false
	}

	static fn encode(ft ast.FunctionType) -> (descriptor string) {
		descriptor = "("
		for _, v := range ft.parameterList {
			descriptor += LucyFieldSignature.encode(v.Type)
		}
		if ft.vArgs != null {
			descriptor += LucyFieldSignature.encode(ft.vArgs.Type)
		}
		descriptor += ")"
		if ft.voidReturn() {
			descriptor += "V"
		} else {
			descriptor += "("
			for _, v := range ft.returnList {
				descriptor += LucyFieldSignature.encode(v.Type)
			}
			descriptor += ")"
		}
		return descriptor
	}

	//rewrite types
	public static fn decode(ft ast.FunctionType, bs []byte)-> (bsSliced []byte,err  error) {
		bs = bs[1:] // skip (
		if len(ft.parameterList) > 0 {
			ft.parameterList = null
		}
		ft.parameterList = []ast.Variable{}
		for bs[0] != ')' {
			var t ast.Type
			bs, t, err = LucyFieldSignature.decode(bs)
			if err != null {
				return bs, err
			}
			vd := new ast.Variable()
			vd.Type = t
			ft.parameterList.append(vd)
		}
		if bs[0] != ')' {
			return bs, new error(sprintf("function type format wrong"))
		}
		bs = bs[1:] // skip )
		if bs[0] == '(' {
			bs = bs[1:]
			ft.returnList = []ast.Variable{}
			for bs[0] != ')' {
				v := new ast.Variable()
				var t ast.Type
				bs, t, err = LucyFieldSignature.decode(bs)
				if err != null {
					return bs, err
				}
				v.Type = t
				ft.returnList.append(v)
			}
			bs = bs[1:] // skip )
		} else if bs[0] == 'V' {
			bs = bs[1:] // skip V
			ft.returnList = new []ast.Variable(1)
			ft.returnList[0] = new ast.Variable()
			ft.returnList[0].name = ""
			ft.returnList[0].Type = new ast.Type()
			ft.returnList[0].Type.Type = ast.VariableTypeVoid
		} else {
			return bs, new error(sprintf("function type format wrong"))
		}
		return bs, null
	}
}
