
import "lucy/binary"

public class AttributeDefaultParameters {
	public start     char // start
	public constants []char

	fn fromBytes(bs []byte) {
	    this.constants = []char{}
		this.start = binary.BigEndian.getChar(bs)
		bs = bs[2:]
		for len(bs) > 0 {
			this.constants.append(binary.BigEndian.getChar(bs))
			bs = bs[2:]
		}
	}

	fn AttributeDefaultParameters(start char , Constants []char){
		this.super()
		this.start = start
		this.constants = Constants
	}

	fn ToAttributeInfo(c Class) -> (info AttributeInfo) {
		if  len(this.constants) == 0 {
			return null
		}
		info = new AttributeInfo()
		info.nameIndex = c.InsertUtf8Const(AttributeNameLucyDefaultParameters)
		info.attributeLength = (2 * (1 + len(this.constants)))
		info.Info = new []byte(info.attributeLength)
		binary.BigEndian.putChar(info.Info, this.start)
		for i := 0; i < len(this.constants); i++ {
			binary.BigEndian.putChar(info.Info[(i+1)*2:], this.constants[i])
		}
		return info
	}

}












