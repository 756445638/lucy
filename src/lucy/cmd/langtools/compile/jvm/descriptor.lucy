import "lucy/cmd/langtools/compile/ast"
import "lucy/cmd/langtools/compile/common"

public class Descriptor  {


	static fn methodDescriptor(functionType ast.FunctionType) -> (s string) {
		s = "("
		for _, v := range functionType.ParameterList {
			s += .typeDescriptor(v.Type)
		}
		if functionType.VArgs != null {
			s += .typeDescriptor(functionType.VArgs.Type)
		}
		s += ")"
		if functionType.VoidReturn() {
			s += "V"
		} else if len(functionType.ReturnList) == 1 {
			s += .typeDescriptor(functionType.ReturnList[0].Type)
		} else {
			s += "[Ljava/lang/Object;" //always this type
		}
		return s
	}

	static fn typeDescriptor(typ ast.Type) -> (d string) {
		switch typ.Type {
		case ast.VariableTypeBool:
			return "Z"
		case ast.VariableTypeByte:
			return "B"
		case ast.VariableTypeShort:
			return "S"
		case ast.VariableTypeChar:
			return "C"
		case ast.VariableTypeInt, ast.VariableTypeEnum:
			return "I"
		case ast.VariableTypeLong:
			return "J"
		case ast.VariableTypeFloat:
			return "F"
		case ast.VariableTypeDouble:
			return "D"
		case ast.VariableTypeArray:
			meta := ArrayMetas[typ.array.Type] // combination type
			return "L" + meta.className + ";"
		case ast.VariableTypeString:
			return "Ljava/lang/String;"
		case ast.VariableTypeVoid:
			return "V"
		case ast.VariableTypeObject:
			return "L" + typ.Class.name + ";"
		case ast.VariableTypeMap:
			return "L" + mapClass + ";"
		case ast.VariableTypeFunction:
			return "L" + javaMethodHandleClass + ";"
		case ast.VariableTypeJavaArray:
			return "[" + .typeDescriptor(typ.array)
		}
		panic("unHandle type signature")
	}

	public static fn ParseType(bs []byte) -> (bsSlieced []byte, t ast.Type,err error) {
		switch bs[0] {
		case 'V':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeVoid
			return bs , t ,err
		case 'B':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeByte
			return bs , t ,err
		case 'D':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeDouble
			return bs , t ,err
		case 'F':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeFloat
			return bs , t ,err
		case 'I':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeInt
			return bs , t ,err
		case 'J':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeLong
			return bs , t ,err
		case 'C':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeChar
			return bs , t ,err
		case 'S':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeShort
			return bs , t ,err
		case 'Z':
			bs = bs[1:]
			t = new ast.Type()
			t .Type = ast.VariableTypeBool
			return bs , t ,err
		case 'L':
			bs = bs[1:]
			index := common.Bytes.indexByte(bs, ';') // end token
			t := new ast.Type()
			t.Type = ast.VariableTypeObject
			t.Class = new ast.Class()
			t.Class.name = string(bs[:index])
			bs = bs[index+1:] // skip ;
			t.Class.notImportedYet = true
			if t.Class.name == javaStringClass {
				t.Type = ast.VariableTypeString
			}
			return bs, t, null
		case '[':
			bs = bs[1:]
			var t ast.Type
			var err error
			bs, t, err = .ParseType(bs)
			ret := new ast.Type()
			if err == null {
				ret.Type = ast.VariableTypeJavaArray
				ret.array = t
			}
			return bs, ret, err
		}
		return bs, null, new error(sprintf("unkown type:%v", string(bs)))
	}

	public static fn ParseFunctionType(bs []byte) -> (t ast.FunctionType,err error) {
		t = new ast.FunctionType()
		if bs[0] != '(' {
			return t, new error(sprintf("function descriptor does not start with '('"))
		}
		bs = bs[1:]
		t.ParameterList = []ast.Variable{}
		for bs[0] != ')' {
			vd := new ast.Variable()
			bs, vd.Type, err = .ParseType(bs)
			if err != null {
				return t, err
			}
			t.ParameterList.append(vd)
		}
		bs = bs[1:] // skip )
		vd := new ast.Variable()
		vd.name = "returnValue"
		_, vd.Type, err = .ParseType(bs)
		if err != null {
			return t, err
		}
		t.ReturnList = [vd]
		return t, null
	}


}

