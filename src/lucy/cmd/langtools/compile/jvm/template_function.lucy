
import "lucy/cmd/langtools/compile/ast"
import "lucy/cmd/langtools/compile/jvm/cg"

class BuildTemplateFunctionCall {

    fn buildTemplateFunctionCall(
        c cg.ClassHighLevel,
        code cg.AttributeCode,
        e ast.Expression,
        context Context,
        state StackMapState) -> (maxStack char) {
        call := e.data.(ast.ExpressionFunctionCall)
        if call.TemplateFunctionCallPair.Entrance == null {
            method := new cg.MethodHighLevel()
            method.Class = c
            method.name = c.newMethodName(nameTemplateFunction(call.TemplateFunctionCallPair.Function))
            method.accessFlags |= cg.AccClassPublic
            method.accessFlags |= cg.AccClassFinal
            method.accessFlags |= cg.AccMethodStatic
            method.accessFlags |= cg.AccMethodBridge
            if call.TemplateFunctionCallPair.Function.Type.VArgs != null {
                method.accessFlags |= cg.AccMethodVarargs
            }
            method.Descriptor = Descriptor.methodDescriptor(call.TemplateFunctionCallPair.Function.Type)
            method.code = new cg.AttributeCode()
            c.appendMethod(method)
            call.TemplateFunctionCallPair.Function.Entrance = method
            //build function
            this.BuildPackage.buildFunction(c, null, method, call.TemplateFunctionCallPair.Function)
            call.TemplateFunctionCallPair.Entrance = method
        }
        maxStack = this.eb.BuildExpressionCallArgs.buildCallArgs(c, code, call.Args, call.VArgs, context, state)
        code.Codes[code.CodeLength] = cg.OP_invokestatic
        c.insertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
            call.TemplateFunctionCallPair.Entrance.Class.name,
            call.TemplateFunctionCallPair.Entrance.name,
            call.TemplateFunctionCallPair.Entrance.Descriptor
        ),
            code.Codes[code.CodeLength+1:code.CodeLength+3])
        code.CodeLength += 3
        return
    }


}


