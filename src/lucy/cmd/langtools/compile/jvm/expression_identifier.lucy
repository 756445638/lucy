// all rights reserved , check the LICENSE file

import "lucy/cmd/langtools/compile/ast"
import "lucy/jvm/cg"

class  BuildExpressionIdentifier {
	

	eb BuildExpression
    
    fn BuildExpressionIdentifier(eb BuildExpression){
        this.super()
        this.eb = eb
    }

	fn  buildCapturedIdentifier(
		c cg.ClassHighLevel,
		code cg.AttributeCode,
		e ast.Expression,
		context Context) -> (maxStack char) {
		identifier := e.data.(ast.ExpressionIdentifier)
		maxStack = jvmSlotSize(identifier.variable.Type)
		if context.theFunction.closure.closureVariableExist(identifier.variable) {
			if identifier.variable.beenCapturedAsLeftValue > 0 {
				copyOPs(code, loadLocalVariableOps(ast.VariableTypeObject, char(0)))
				meta := closure.getMeta(identifier.variable.Type.Type)
				code.codes[code.codeLength] = cg.OP_getfield
				c.insertFieldRefConst(new cg.ConstantInfoFieldrefHighLevel(
					c.name,
					identifier.variable.name,
					"L" + meta.className + ";"
				), code.codes[code.codeLength+1:code.codeLength+3])
				code.codeLength += 3
				closure.unPack(c, code, identifier.variable.Type)
			} else {
				copyOPs(code, loadLocalVariableOps(ast.VariableTypeObject, char(0)))
				code.codes[code.codeLength] = cg.OP_getfield
				c.insertFieldRefConst(new cg.ConstantInfoFieldrefHighLevel(
					c.name,
					identifier.variable.name,
					Descriptor.typeDescriptor(identifier.variable.Type)
				), code.codes[code.codeLength+1:code.codeLength+3])
				code.codeLength += 3
			}
		} else {
			if identifier.variable.beenCapturedAsLeftValue > 0 {
				copyOPs(code, loadLocalVariableOps(ast.VariableTypeObject, identifier.variable.localValOffset))
				closure.unPack(c, code, identifier.variable.Type)
			} else {
				copyOPs(code, loadLocalVariableOps(e.value.Type, identifier.variable.localValOffset))
			}
		}
		return
	}

	fn  buildIdentifier(
		c cg.ClassHighLevel,
		code cg.AttributeCode,
		e ast.Expression,
		context Context) -> (maxStack char) {
		if e.value.Type == ast.VariableTypeClass {
			panic("this is not happening")
		}
		identifier := e.data.(ast.ExpressionIdentifier)
		switch identifier{
		case e.value.Type == ast.VariableTypeEnum && identifier.enumName != null:
			loadInt(c, code, identifier.enumName.value)
			maxStack = char(1)
			return
		case identifier.function != null:
			return this.eb.BuildExpressionFunction.packFunction2MethodHandle(c, code, identifier.function, context)
		case identifier.variable.isGlobal:
			code.codes[code.codeLength] = cg.OP_getstatic
			c.insertFieldRefConst(new cg.ConstantInfoFieldrefHighLevel(
				this.eb.bp.mainClass.name,
				identifier.name,
				Descriptor.typeDescriptor(identifier.variable.Type)
			), code.codes[code.codeLength+1:code.codeLength+3])
			code.codeLength += 3
			maxStack = jvmSlotSize(identifier.variable.Type)
			return
		case identifier.variable.beenCapturedAsLeftValue+identifier.variable.beenCapturedAsRightValue > 0:
			return this.buildCapturedIdentifier(c, code, e, context)
		default:
			maxStack = jvmSlotSize(e.value)
			copyOPs(code, loadLocalVariableOps(e.value.Type, identifier.variable.localValOffset))
			return
		}
	}	
}

