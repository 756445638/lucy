 
import "lucy/cmd/langtools/compile/ast"
import "lucy/cmd/langtools/compile/jvm/cg"


class BuildinFunctionPrintf {



    eb BuildExpression
    
    fn BuildinFunctionPrintf(eb BuildExpression){
        this.super()
        this.eb = eb
    }

    /*
        function printf
    */
    fn buildPrintf(
        c cg.ClassHighLevel,
        code cg.AttributeCode,
        e ast.Expression,
        context Context,
        state StackMapState) -> (maxStack char) {
        length := len(state.stacks)
        defer {
            state.popStack(len(state.stacks) - length)
        }
        call := e.data.(ast.ExpressionFunctionCall)
        meta := call.buildInFunctionMeta.(ast.BuildInFunctionPrintfMeta)
        code.codes[code.codeLength] = cg.OP_getstatic
        c.insertFieldRefConst(new cg.ConstantInfoFieldrefHighLevel(
                 "java/lang/System",
                "out",
             "Ljava/io/PrintStream;"
        ), code.codes[code.codeLength+1:code.codeLength+3])
        code.codeLength += 3
        maxStack = 1
        state.pushStack(c, state.newObjectVariableType(javaPrintStreamClass))
        stack := this.eb.build(c, code, meta.format, context, state)
        if t := char(1) + stack; t > maxStack {
            maxStack = t
        }
        state.pushStack(c, state.newObjectVariableType(javaStringClass))
        loadInt(c, code, int(meta.argsLength))
        code.codes[code.codeLength] = cg.OP_anewarray
        c.insertClassConst("java/lang/Object", code.codes[code.codeLength+1:code.codeLength+3])
        code.codeLength += 3
        currentStack := char(3)
        if currentStack > maxStack {
            maxStack = currentStack
        }
        objectArray := new ast.Type()
        objectArray.Type = ast.VariableTypeJavaArray
        objectArray.array = state.newObjectVariableType(javaRootClass)
        state.pushStack(c, objectArray)
        index := int(0)
        for _, v := range call.args {
            currentStack = 3
            code.codes[code.codeLength] = cg.OP_dup
            code.codeLength++
            loadInt(c, code, index)
            currentStack += 2
            state.pushStack(c, objectArray)
            state.pushStack(c, typeInt)
            stack := this.eb.build(c, code, v, context, state)
            state.popStack(2)
            if t := currentStack + stack; t > maxStack {
                maxStack = t
            }
            if v.value.isPointer() == false {
                TypeConverterAndPrimitivePacker.packPrimitives(c, code, v.value)
            }
            code.codes[code.codeLength] = cg.OP_aastore
            code.codeLength++
            index++
        }
        code.codes[code.codeLength] = cg.OP_invokevirtual
        c.insertMethodRefConst(new cg.ConstantInfoMethodrefHighLevel(
                javaPrintStreamClass,
              "printf",
             "(Ljava/lang/String;[Ljava/lang/Object;)Ljava/io/PrintStream;"
        ),
            code.codes[code.codeLength+1:code.codeLength+3])
        code.codes[code.codeLength+3] = cg.OP_pop
        code.codeLength += 4
        return
    }


}


