import "lucy/cmd/langtools/compile/ast"
import "lucy/jvm/cg"


class BuildExpressionUnary{
    
    eb BuildExpression
    
    fn BuildExpressionUnary(eb BuildExpression){
        this.super()
        this.eb = eb
    }
    
    fn buildUnary(
        c cg.ClassHighLevel,
        code cg.AttributeCode,
        e ast.Expression,
        context Context,
        state StackMapState) -> (maxStack char) {

        if e.Type == ast.ExpressionTypeNegative {
            ee := e.data.(ast.Expression)
            maxStack = this.eb.build(c, code, ee, context, state)
            switch e.value.Type {
            case ast.VariableTypeByte,ast.VariableTypeShort ,ast.VariableTypeChar ,ast.VariableTypeInt  :
                code.codes[code.codeLength] = cg.OP_ineg
            case ast.VariableTypeFloat:
                code.codes[code.codeLength] = cg.OP_fneg
            case ast.VariableTypeDouble:
                code.codes[code.codeLength] = cg.OP_dneg
            case ast.VariableTypeLong:
                code.codes[code.codeLength] = cg.OP_lneg
            }
            code.codeLength++
            return
        }
        if e.Type == ast.ExpressionTypeBitwiseNot {
            ee := e.data.(ast.Expression)
            maxStack = this.eb.build(c, code, ee, context, state)
            if t := jvmSlotSize(ee.value) * 2; t > maxStack {
                maxStack = t
            }
            switch e.value.Type {
            case ast.VariableTypeByte:
                code.codes[code.codeLength] = cg.OP_bipush
                code.codes[code.codeLength+1] = 255
                code.codes[code.codeLength+2] = cg.OP_ixor
                code.codeLength += 3
                if 2 > maxStack {
                    maxStack = 2
                }
            case ast.VariableTypeChar , ast.VariableTypeShort :
                code.codes[code.codeLength] = cg.OP_sipush
                code.codes[code.codeLength+1] = 255
                code.codes[code.codeLength+2] = 255
                code.codes[code.codeLength+3] = cg.OP_ixor
                code.codeLength += 4
                if 2 > maxStack {
                    maxStack = 2
                }
            case ast.VariableTypeInt:
                code.codes[code.codeLength] = cg.OP_ldc_w
                c.insertIntConst(-1, code.codes[code.codeLength+1:code.codeLength+3])
                code.codes[code.codeLength+3] = cg.OP_ixor
                code.codeLength += 4
                if 2 > maxStack {
                    maxStack = 2
                }
            case ast.VariableTypeLong:
                code.codes[code.codeLength] = cg.OP_ldc2_w
                c.insertLongConst(-1L, code.codes[code.codeLength+1:code.codeLength+3])
                code.codes[code.codeLength+3] = cg.OP_lxor
                code.codeLength += 4
                if 4 > maxStack {
                    maxStack = 4
                }
            }
            return
        }
        if e.Type == ast.ExpressionTypeNot {
            ee := e.data.(ast.Expression)
            maxStack = this.eb.build(c, code, ee, context, state)
            exit := new cg.Exit(cg.OP_ifeq, code)
            code.codes[code.codeLength] = cg.OP_iconst_0
            code.codeLength++
            exit2 := new cg.Exit(cg.OP_goto, code)
            context.MakeStackMap(code, state, code.codeLength)
            writeExits([]cg.Exit{exit}, code.codeLength)
            code.codes[code.codeLength] = cg.OP_iconst_1
            code.codeLength++
            state.pushStack(c, ee.value)
            defer state.popStack(1)
            writeExits([]cg.Exit{exit2}, code.codeLength)
            context.MakeStackMap(code, state, code.codeLength)
        }
        return
    }


}

