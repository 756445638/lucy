class ExpressionCheckUnary {
	

	static fn checkUnaryExpression(e Expression ， block Block, errs []error)  ->(valueType Type) {
		ee := e.Data.(Expression)
		unary, es := ee.checkSingleValueContextExpression(block)
		errs.appendAll(es)
		if unary == null {
			// !a , looks like a bool
			if e.Type == ExpressionTypeNot {
				return &Type{
					Type: VariableTypeBool,
					Pos:  e.Pos,
				}
			}
			return null
		}
		if err := unary.rightValueValid(); err != null {
			errs.append(err)
			return null
		}
		if e.Type == ExpressionTypeNot {
			if unary.Type != VariableTypeBool {
				errs . append(new error( sprintf("%s not a bool expression , but '%s'",
					unary.Pos.ErrMsgPrefix(), unary.TypeString())))
			}
		}
		if e.Type == ExpressionTypeNegative {
			if unary.IsNumber() == false {
				errs.append(new error( sprintf("%s cannot apply '-' on '%s'",
					unary.Pos.ErrMsgPrefix(), unary.TypeString())))
			}
		}
		if e.Type == ExpressionTypeBitwiseNot {
			if unary.isInteger() == false {
				errs . append(new error( sprintf("%s cannot apply '~' on '%s'",
					unary.Pos.ErrMsgPrefix(), unary.TypeString())))
			}
		}
		result := unary.Clone()
		result.Pos = e.Pos
		return result
	}

	static fn checkIncrementExpression(e Expression ， block Block, errs []error) ->(valueType Type) {
		on := e.Data.(Expression)
		increment := on.getLeftValue(block, errs)
		if increment == null {
			return null
		}
		if on.Type == ExpressionTypeIdentifier &&
			e.IsStatementExpression == false {
			/*
				special case
				fn1(a++)
			*/
			t := on.Data.(*ExpressionIdentifier)
			if t.Variable != null {
				t.Variable.Used = true
			}
		}
		if false == increment.IsNumber() {
			errs . append(new error( sprintf("%s cannot apply '%s' on '%s'",
					on.Pos.ErrMsgPrefix(), on.Op, increment.TypeString())))
		}
		result := increment.Clone()
		result.Pos = e.Pos
		return result
	}	
} 

