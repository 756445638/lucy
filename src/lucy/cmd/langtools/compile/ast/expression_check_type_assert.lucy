// all rights reserved , check the LICENSE file

class ExpressionCheckTypeAssert {
    static fn check(e Expression , block Block , errs []error) -> (valueTypes []Type) {
        assertion := e.data.(ExpressionTypeAssert)
        object , es := assertion.value.checkSingleValueContextExpression(block)
        errs.appendAll(es)
        if object == null {
            return
        }
        if err := object.rightValueValid() ; err != null {
            errs.append(err)
            return
        }
        if assertion.isType {
            return [object]
        }
        if object.isPointer() == false {
            errs.append(new error(sprintf("%s expression is not pointer",
                        errMsgPrefix(object.pos))))
            return
        }
        err := assertion.to.resolve(block)
        if err != null {
            errs.append(err)
        } else {
            if object.assignAble(block , errs , assertion.to) == false {
                errs.append(
                    new error(
                        sprintf("%s '%s' is not sub class of '%s'",
                            assertion.to.pos.errMsgPrefix() , assertion.to.typeString() , object.typeString())))
            }
            if assertion.to.validForTypeAssertOrConversion() == false {
                errs.append(new error(sprintf("%s cannot use '%s' for type assertion",
                            errMsgPrefix(assertion.to.pos) , assertion.to.typeString())))
            }
        }
        if len(e.lefts) > 1 {
            assertion.multiValueContext = true
            valueTypes = new []Type(2)
            valueTypes[0] = assertion.to.cloneType()
            valueTypes[0].pos = e.pos
            valueTypes[1] = new Type()
            valueTypes[1].pos = e.pos
            valueTypes[1].Type = VariableTypeBool // if  assert is ok
        } else {
            valueTypes = new []Type(1)
            valueTypes[0] = assertion.to.cloneType()
            valueTypes[0].pos = e.pos
        }

    }
}


