import "lucy/cmd/langtools/compile/jvm/cg"

class ExpressionCheckLeftValue {
	

	
	static fn getLeftValue(e Expression, block Block, errs []error) -> (result Type) {
		switch e.Type {
		case ExpressionTypeIdentifier:
			identifier := e.data.(ExpressionIdentifier)
			if identifier.name == UnderScore {
				errs.append(new error(sprintf("%s cannot use '%s' as left value",
					e.pos.errMsgPrefix(), identifier.name)))
				return null
			}
			if identifier.name == ThisPointerName {
				errs.append(new error(sprintf("%s '%s' cannot be used as left value",
					e.pos.errMsgPrefix(), ThisPointerName)))
			}
			isCaptureVar := [false]
			d, err := block.searchIdentifier(e.pos, identifier.name, isCaptureVar)
			if err != null {
				errs.append(err)
				return null
			}
			if d == null {
				errs.append(new error(sprintf("%s '%s' not found",
					e.pos.errMsgPrefix(), identifier.name)))
				return null
			}
			if v := d.(Variable) ; v != null {
                if isCaptureVar[0] {
                    v.beenCapturedAsLeftValue++
                }
                // variable is modifying , capture right value should not be ok
                // if no variable not change,after been captured, right value should ok too
                v.beenCapturedAsLeftValue += v.beenCapturedAsRightValue
                v.beenCapturedAsRightValue = 0
                identifier.Variable = v
                result = identifier.Variable.Type.Clone()
                result.pos = e.pos
                e.value = result
                return result
			}
            errs.append(new error(sprintf("%s identifier '%s' is '%s' , cannot be used as left value",
                e.pos.errMsgPrefix(), identifier.name, block.identifierIsWhat(d))))
            return null
		case ExpressionTypeIndex:
			result = ExpressionCheckIndex.check(e , block, errs)
			e.value = result
			return result
		case ExpressionTypeSelection:
			selection := e.data.(ExpressionSelection)
			object, es := selection.Expression.checkSingleValueContextExpression(block)
			errs.appendAll(es)
			if object == null {
				return null
			}
			switch object.Type {
			case VariableTypeDynamicSelector:
				if selection.name == SUPER {
					errs.append(new error(sprintf("%s access '%s' at '%s' not allow",
						e.pos.errMsgPrefix(), SUPER, object.typeString())))
					return null
				}
				field, err := object.Class.getField(e.pos, selection.name, false)
				if err != null {
					errs.append(err)
				}
				if field == null {
					return null
				}
				selection.Field = field
				result = field.Type.Clone()
				result.pos = e.pos
				e.value = result
				return result
			case VariableTypeObject, VariableTypeClass:
				field, err := object.Class.getField(e.pos, selection.name, false)
				if err != null {
					errs.append(err)
				}
				selection.Field = field
				if field != null {
					err := selection.Expression.fieldAccessAble(block, field)
					if err != null {
						errs.append(err)
					}
					result = field.Type.Clone()
					result.pos = e.pos
					e.value = result
					return result
				}
				return null
			case VariableTypePackage:
				variable, exists := object.Package.block.nameExists(selection.name)
				if exists == false {
					errs.append(new error(sprintf("%s '%s.%s' not found",
						e.pos.errMsgPrefix(), object.Package.name, selection.name)))
					return null
				}
				if v := variable.(Variable) ;v != null {
				    if v.accessFlags&cg.AccFieldPublic == 0 &&
                        object.Package.isSame(PackageBeenCompile) == false {
                        errs.append(new error(sprintf("%s '%s.%s' is private",
                            e.pos.errMsgPrefix(), object.Package.name, selection.name)))
                    }
                    selection.PackageVariable = v
                    result = v.Type.Clone()
                    result.pos = e.pos
                    e.value = result
                    return result
				}

                errs.append(new error(sprintf("%s '%s' is not variable",
                    e.pos.errMsgPrefix(), selection.name)))
                return null

			case VariableTypeMagicFunction:
				v := object.Function.Type.searchName(selection.name)
				if v == null {
					err := new error(sprintf("%s '%s' not found", e.pos.errMsgPrefix(), selection.name))
					errs.append(err)
					return null
				}
				e.value = v.Type.Clone()
				e.value.pos = e.pos
				e.Type = ExpressionTypeIdentifier
				identifier := new ExpressionIdentifier()
				identifier.name = selection.name
				identifier.Variable = v
				e.data = identifier
				result := v.Type.Clone()
				result.pos = e.pos
				return result
			default:
				errs.append(new error(sprintf("%s cannot access '%s' on '%s'",
					e.pos.errMsgPrefix(), selection.name, object.typeString())))
				return null
			}
		default:
			errs.append(new error(sprintf("%s '%s' cannot be used as left value",
				e.pos.errMsgPrefix(),
				e.op)))
			return null
		}
		return null
	}

} 
