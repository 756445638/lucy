import "lucy/cmd/langtools/compile/jvm/cg"

public class StatementIf {
	public PrefixExpressions   []Expression
	public Condition           Expression
	public Pos                 Pos
	public initExpressionBlock Block = new Block()
	public Block               Block
	public ElseIfList          []StatementElseIf
	public Else               	Block
	public Exits               []cg.Exit
	
	fn check(father Block) -> (errs []error = []error{})  {
		this.initExpressionBlock.inherit(father)
		for _, v := range this.PrefixExpressions {
			v.IsStatementExpression = true
			_, es := v.check(this.initExpressionBlock)
			errs.appendAll(es)
			if err := v.canBeUsedAsStatement(); err != null {
				errs.append(err)
			}
		}
		if this.Condition != null {
			conditionType, es := this.Condition.checkSingleValueContextExpression(this.initExpressionBlock)
			errs.appendAll(es)
			if conditionType != null &&
				conditionType.Type != VariableTypeBool {
				errs.append(new error(sprintf("%s condition is not a bool expression",
					this.Condition.Pos.ErrMsgPrefix())))
			}
			if err := this.Condition.canBeUsedAsCondition(); err != null {
				errs.append(err)
			}
		}
		this.Block.inherit(this.initExpressionBlock)
		errs.appendAll(this.Block.check())
		for _, v := range this.ElseIfList {
			v.Block.inherit(this.initExpressionBlock)
			if v.Condition != null {
				conditionType, es := v.Condition.checkSingleValueContextExpression(v.Block)
				errs.appendAll(es)
				if err := v.Condition.canBeUsedAsCondition(); err != null {
					errs.append(err)
				}
				if conditionType != null &&
					conditionType.Type != VariableTypeBool {
					errs.append(new error(sprintf("%s condition is not a bool expression",
							conditionType.Pos.ErrMsgPrefix())))
				}
				errs.appendAll(v.Block.check())
			}
		}
		if this.Else != null {
			this.Else.inherit(this.initExpressionBlock)
			errs.appendAll(this.Else.check())
		}
		return errs
	}
}



public class StatementElseIf {
	public Condition Expression
	public Block     Block
}
