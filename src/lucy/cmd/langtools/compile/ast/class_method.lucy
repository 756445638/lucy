import "lucy/cmd/langtools/compile/jvm/cg"

public class ClassMethod {
	public IsCompilerAuto bool
	public Function       Function

	fn narrowDownAccessRange(implementation ClassMethod) ->(is bool ) {
		if this.IsPublic() {
			return !implementation.IsPublic()
		}
		if this.IsProtected() {
			return implementation.IsPrivate() ||
				implementation.isAccessFlagDefault()
		}
		if this.isAccessFlagDefault() {
			return implementation.IsPrivate()
		}
		return false
	}
	
	fn accessString() ->(s string) {
		if this.IsPublic() {
			return "public"
		}
		if this.IsProtected() {
			return "protected"
		}
		if this.IsPrivate() {
			return "private"
		}
		return `default ""`
	}

	fn isAccessFlagDefault() ->(is bool ) {
		return this.Function.AccessFlags&
			(cg.AccMethodPublic|
				cg.AccMethodPrivate|
				cg.AccMethodProtected) == 0
	}

	fn IsPublic() ->(is bool ) {
		return (this.Function.AccessFlags & cg.AccMethodPublic) != 0
	}
	fn IsProtected() ->(is bool ) {
		return (this.Function.AccessFlags & cg.AccMethodProtected) != 0
	}

	fn IsStatic() ->(is bool ) {
		return (this.Function.AccessFlags & cg.AccMethodStatic) != 0
	}

	fn IsPrivate() ->(is bool ) {
		return (this.Function.AccessFlags & cg.AccMethodPrivate) != 0
	}
	fn IsFinal() ->(is bool ) {
		return (this.Function.AccessFlags & cg.AccMethodFinal) != 0
	}
	fn IsAbstract() ->(is bool ) {
		return (this.Function.AccessFlags & cg.AccMethodAbstract) != 0
	}

	fn ableAccessFromSubClass() ->(is bool ) {
		return this.IsPublic() ||
			this.IsProtected()
	}

	fn checkModifierOk() ->(errs []error = []error{}) {
		if this.IsAbstract() && this.IsFinal() {
			errs.append(new error(sprintf("%s abstract method cannot be final",
				errMsgPrefix(this.Function.Pos))))
		}
		if this.IsAbstract() && this.IsPrivate() {
			errs.append( new error(sprintf("%s abstract method cannot be private",
				errMsgPrefix(this.Function.Pos))))
		}
		if this.IsAbstract() && this.Function.Name == SpecialMethodInit {
			errs.append( new error(sprintf("%s construction method cannot be abstract",
				errMsgPrefix(this.Function.Pos))))
		}
		return errs
	}

	fn IsFirstStatementCallFatherConstruction() ->(is bool ) {
		if len(this.Function.Block.Statements) == 0 {
			return false
		}
		s := this.Function.Block.Statements[0]
		if s.Type != StatementTypeExpression {
			return false
		}
		e := s.Expression
		if e.Type != ExpressionTypeMethodCall {
			return false
		}
		call := s.Expression.Data.(ExpressionMethodCall)
		if call.Expression.IsIdentifier(ThisPointerName) == false || call.Name != SUPER {
			return false
		}
		return true
	}



	fn implementationMethodIsOk(
		pos Pos,
		implementation ClassMethod) ->(err error) {
		if implementation.Function.Pos != null {
			pos = implementation.Function.Pos
		}
		if implementation.IsStatic() {
			return new error(sprintf("%s method '%s' is static", pos.ErrMsgPrefix(), this.Function.Name))
		}
		if this.narrowDownAccessRange(implementation) {
			return new error(sprintf("%s implementation of method '%s' should not narrow down access range, '%s' -> '%s'",
				pos.ErrMsgPrefix(), this.Function.Name, this.accessString(), implementation.accessString()))
		}
		return null
	}	
}

