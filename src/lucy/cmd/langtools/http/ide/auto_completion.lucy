import "lucy/cmd/langtools/ide/autocompletion"
import "com/sun/net/httpserver/HttpHandler"
import "com/sun/net/httpserver/HttpExchange"
import "lucy/cmd/langtools/http/g"
import "java/lang/Integer"
import "lucy/encoding/json"
import "lucy/cmd/langtools/compile/ast"
import "java/lang/System"


class AutoCompletion implements HttpHandler {
    public fn handle(exchange HttpExchange) {
        os := exchange.getResponseBody()
        now := System.currentTimeMillis()
        defer {
            CallStatus.collect(callStatus , "autocompletion" , System.currentTimeMillis() - now)
            e := catch()
            if e != null {
                exchange.sendResponseHeaders(500 , 0L)
                os.write(byte[]("internal error"))
                print("call auto completion failed")
                e.printStackTrace()
            }
            exchange.close()
        }
        uri := exchange.getRequestURI()
        paramters := g.parseQuery(uri.getQuery())
        file := paramters["file"]
        if file == null || file == ""{
            bs := byte[]("file not provide")
            exchange.sendResponseHeaders(400 , long(len(bs)))
            os.write(bs)
            return
        }
        line := Integer.parseInt(paramters["line"])
        column := Integer.parseInt(paramters["column"])
        bs := g.readBodyBytes(exchange.getRequestBody())
        if len(bs) == 0 {
            bs := byte[]("zero length bs")
            exchange.sendResponseHeaders(400 , long(len(bs)))
            os.write(bs)
            return
        }
        usages := autocompletion.autoCompletion(file , (line << 16) | int(char(column)) ,bs)
        result := copy(new ast.AutoCompletionItem[](len(usages)) , usages)
        bs,_ = json.encode(result , "\t")
        exchange.sendResponseHeaders(200 , long(len(bs)))
        os.write(copy(new byte[](len(bs)) ,bs )) 
    } 
}
