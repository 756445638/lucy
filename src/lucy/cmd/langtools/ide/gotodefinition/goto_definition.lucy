import "lucy/cmd/langtools/compile/parser"
import "lucy/cmd/langtools/ide/utils"
import "java/io"
import "java/nio/file/FileSystems"
import "lucy/encoding/json"
import "lucy/cmd/langtools/compile/ast"

public fn findDefinition(args []string) -> (result ast.Pos) {
    f := new Flags()
    file , pos := f.parseArgs(args)
    lucyPath , packageName := utils.locateLucyFile(file)
    var lucyFiles []utils.LucyFile 
    {
        t := []string{}
        p := lucyPath + "/" + packageName
        for v := range new io.File(p).list() {
            if v.endsWith(".lucy") {
                t.append(p + "/" + v)
            }
        }
        lucyFiles = utils.readLucyFiles(t)
    }
    fileSystem := FileSystems.getDefault()
    for v := range lucyFiles {
        if fileSystem.getPath(file).getFileName().toString() == 
            fileSystem.getPath(v.realPathName).getFileName().toString(){
            v.locateDefinition = pos 
        }
    }
    result = new FindDefinition(lucyFiles,packageName , true ).find()
}

public fn main(args []string) {
    bs , err := json.encode(findDefinition(args))
    if err != null {
        panic(err)
    }
    print(string(bs))
}