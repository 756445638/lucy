
import "lucy/cmd/langtools/compile/ast"

public class Fmt {
    public fn fmt(nodes []ast.TopNode) -> (format string = ""){
        for k, n := range nodes {
            if t,ok := n.node.(ast.Comment) ; ok {
                if t.isMulti {
                    format += "/*" +  t.comment
                    if t.comment[len(t.comment) -1] == '\n' {
                        format +="*/" + "\n"
                    }else {
                        format += "\n" + "*/" + "\n"
                    }
                }else {
                    format += "//" + t.comment // contains \n
                }
            }
            if t,ok := n.node.(ast.Block) ; ok {
                format += this.formatBlock("" , t) + "\n"
            }
            if t,ok := n.node.(ast.Function) ; ok {
                format += this.formatFunction("" , t ,false ) + "\n"
            }
            if t,ok := n.node.(ast.Enum) ; ok {
                format += this.formatEnum("" , t) + "\n"
            }
            if t,ok := n.node.(ast.Class) ; ok {
                format += this.formatClass("" , t) + "\n"
            }
            if t,ok := n.node.(ast.Constant) ; ok {
                format += this.formatConstant("" , t)  + "\n"
            }
            if t,ok := n.node.(ast.Import) ; ok {
                format += this.formatImport("" , t) + "\n"
            }
            if t,ok := n.node.(ast.Expression) ; ok {
               format += this.formatExpression("" , t) + "\n"
            }
            if t,ok := n.node.(ast.TypeAlias) ; ok {
               format += this.formatTypealais("" , t) + "\n"
            }
        }
    }
    
    fn formatTypealais(indent string , a ast.TypeAlias ) -> (format string = "") {
        format = indent
        format += sprintf ("typealias %s = %s" , a.name ,a.Type.typeName())
    }
    
    fn formatConstant(indent string , c ast.Constant ) -> (format string = "") {
        format = indent
        format += "const " + c.name
        if c.Type != null {
            format += " " + c.Type.typeName()
        }
        format += " =" +  c.defaultValueExpression.literalRaw
    }

    fn formatFunction(indent string , f ast.Function,isAbstract bool) -> (format string = "") {
        format += "fn "
        if f.name != null && f.name != "" {
            format += f.name + " "
        }
        format += "("
        for k , v := range f.Type.parameterList {
            if v.name != null && v.name != "" {
                format += v.name + " "
            }
            format += v.Type.typeName() 
            if k != len(f.Type.parameterList) - 1 {
                format += " , "
            }
        }
        format += ")"
        if f.Type.VoidReturn() == false {
            format += "-> ("
            for k , v := range f.Type.returnList {
                if v.name != null && v.name != "" {
                    format += v.name + " "
                }
                format += v.Type.typeName() 
                if k != len(f.Type.returnList) - 1 {
                    format += " , "
                }
            }
            format += ")" 
        } 
        format += " {\n"
        format += this.formatBlock(indent+"    ", f.block)
        format += indent + "}"
    }


    fn formatClass(indent string , c ast.Class) -> (format string = "") {
        format = indent 
        if c.isPublic() {
            format += "public "
        }
        if c.isFinal() {
            format += "final "
        }
        if c.isAbstract() {
            format += "abstract "
        }
        if c.isInterface() {
            format += "interface "
        }else {
            format += "class "
        }
        format += c.shortName + " {\n"
        for k:= 0 ; k < len(c.fmts) ; k++{
            f := c.fmts[k]
            if t , ok := f.(ast.Comment) ; ok {
                if t.isMulti {
                    format += indent + "    /*\n" + t.comment 
                    format += indent + "    */\n"
                }else {
                    format += indent + "    //" + t.comment  
                }
                continue
            }
            if t,ok := f.(ast.Line) ; ok {
                format += "\n"
                continue
            }
            if field ,ok := f.(ast.ClassFieldDeclaration) ; ok {
                fields := []ast.ClassFieldDeclaration{} 
                {
                    t , ok := c.fmts[k].(ast.ClassFieldDeclaration)
                    for ok {
                        fields.append(t)
                        k++ 
                        t , ok = c.fmts[k].(ast.ClassFieldDeclaration)
                    }
                    k--
                }
                maxName,maxAccessRange , hasFinal , hasStatic := 
                    this.getFieldDeclarationGroupMeta(fields)
                for k,f := range fields {
                    d := indent + "    "
                    if maxAccessRange != "" {
                        accessControl := this.getFieldAccessControlString(f)
                        d += accessControl
                        if len(accessControl) < len(maxAccessRange) {
                            d += this.mkSpacing(len(maxAccessRange) - len(accessControl))
                        }
                        d += " "
                    }
                    if hasFinal {
                        if f.fields[0].isFinal() {
                            d += "final "
                        }else {
                            d += "      "
                        }
                    }
                    if hasStatic {
                        if f.fields[0].isStatic() {
                            d += "static "
                        }else {
                            d += "       "
                        }
                    }
                    name :=  "" 
                    for k , v := range f.nameList {
                        name += v.name 
                        if k != len(f.nameList) -1 {
                            name  += " , "
                        }   
                    }
                    d += name 
                    if t := len(maxName) - len(name) ; t > 0  {
                        d += this.mkSpacing(t)
                    }
                    d += " "
                    d += f.fields[0].Type.typeName()
                    d += "\n"
                    format += d 
                }
                continue
            }
            // must be a method 
            method := f.(ast.ClassMethod)
            if method.isCompilerAuto {
                continue
            }
            m := indent + "    "
            if method.isPublic() {
                m += "public "
            }
            if method.isPrivate() {
                m += "private"
            }
            if method.isProtected() {
                m += "protected "
            }
            if method.isFinal() {
                m += "final "
            }
            if method.isStatic() {
                m += "static "
            }
            if method.isAbstract() {
                m += "abstract "
            }
            m += this.formatFunction( indent + "    ", method.Function , method.isAbstract())
            format += m + "\n"
        }
        format += indent + "}\n"
    }

    fn mkSpacing(length int) ->(s string = "") {
        for i := 0 ;i < length ; i++ {
            s += " "
        }
    }
    fn getFieldAccessControlString(f ast.ClassFieldDeclaration) ->(access string = "") {
        if f.fields[0].isPublic() {
            access = "public"
        }
        if f.fields[0].isProtected() {
            access =  "protected"
        }
        if f.fields[0].isPrivate() {
            access =  "private"
        }

    }

    fn getFieldDeclarationGroupMeta(fs []ast.ClassFieldDeclaration) ->(maxName string = "" , maxAccessRange string = "" , hasFinal bool , hasStatic bool) {
        for f := range fs {
            if f.fields[0].isPublic() {
                maxAccessRange = maxAccessRange == "" ? "public" : maxAccessRange
            }
            if f.fields[0].isPrivate() {
                maxAccessRange = maxAccessRange == "" || maxAccessRange == "public" ? "private" : maxAccessRange
            }
            if f.fields[0].isProtected() {
                maxAccessRange = "protected"
            }
            if f.fields[0].isFinal() {
                hasFinal = true
            }
            if f.fields[0].isStatic() {
                hasStatic = true 
            }
            if name := this.getFieldDeclarationName(f) ; len(name) > len(maxName){
                maxName = name 
            }
        }
    }

    fn getFieldDeclarationName(f ast.ClassFieldDeclaration) ->(name string = "") {
        for kk,ff := range f.nameList {
            name += ff.name 
            if kk != len(f.nameList) - 1 {
                name += " , "
            }
        }
    }


    fn formatEnum(indent string , e ast.Enum) -> (format string = "") {
        format = indent 
        format += "enum " + e.shortName + " {\n"
        for k , f := range e.fmts {
            if t,ok := f.(ast.Comment) ; ok {
                if t.isMulti {
                    format += indent + "    /*" + t.comment 
                    format += indent + "    */\n"
                }else {
                    format += indent + "    //" + t.comment + "\n"
                }
                continue
            }
            if t,ok := f.(ast.Line) ; ok {
                format += "\n"
                continue
            }
            v := f.(ast.EnumName)
            format += "    " + v.name
            if v.NoNeed != null {
                format += " " + this.formatExpression(indent , v.NoNeed)
            }
            isLast := true  
            for en := range e.fmts[k+1:] {
                _ , ok :=  en.(ast.EnumName)
                if ok {
                    isLast = false 
                    break
                } 
            }
            if isLast == false  {
                format += ","
            }
            format += "\n"
        }
        format += indent + "}\n"
    }
    
    
    fn formatImport(indent string , i ast.Import) -> (format string = "") {
        format = indent
        format += "import \"" + i.Import + "\""
        if i.alias != null {
            format += " as " + i.alias 
        }
    }


    fn formatBlock(indent string , block ast.Block) -> (format string = ""){
        for s := range block.statements {
            format += this.formatStatement(indent , s)
        }
    }

    fn formatStatement(indent string , s ast.Statement) -> (format string = ""){
        switch s.Type {
            case ast.StatementTypeExpression:
                format = indent + this.formatExpression(indent , s.Expression) + "\n"
            case ast.StatementTypeIf:
                format += this.formatIf(indent , s.StatementIf)
            case ast.StatementTypeBlock:
                format += this.formatBlock(indent , s.block)
            case ast.StatementTypeFor:
                format += this.formatFor(indent , s.StatementFor)
            case ast.StatementTypeContinue:
                format += "continue" + "\n"
            case ast.StatementTypeReturn:
                format += this.formatReturn(indent , s.StatementReturn)
            case ast.StatementTypeBreak:
                format += "break" + "\n"
            case ast.StatementTypeSwitch:
                format += this.formatSwitch(indent , s.StatementSwitch)
            case ast.StatementTypeWhen:
                format += this.formatWhen(indent , s.StatementWhen)
            case ast.StatementTypeLabel:
                format += s.StatementLabel.name + ":\n"
            case ast.StatementTypeGoTo:
                format += this.formatGoto(indent,s.StatementGoTo)
            case ast.StatementTypeDefer:
                format += this.formatDefer(indent , s.Defer )
            case ast.StatementTypeClass:
                format += this.formatClass(indent , s.Class)
            case ast.StatementTypeEnum:
                format += this.formatEnum(indent , s.Enum)
            case ast.StatementTypeNop:
               //nothing
            case ast.StatementTypeImport:
                format += this.formatImport(indent , s.Import)
            case ast.StatementTypeTypeAlias:
                format += this.formatTypealias(indent , s.TypeAlias)
            case ast.StatementTypeEmptyLine:
                format += "\n"
            case ast.StatementTypeComment:
                format += indent + "//" + s.comment
            case ast.StatementTypeCommentMultiLine:
                format += indent + "/*\n" + s.comment 
                format += indent + "*/\n"   
        }
    }
    fn formatTypealias(indent string ,a ast.TypeAlias)-> (format string = "") {
        format = indent
        format += sprintf("typealias %s = %s" , a.name , a.Type.typeName()) 
    }
    fn formatDefer(indent string ,d ast.StatementDefer)-> (format string = "") {
        format = indent
        format += "defer {"
        format += this.formatBlock(indent + "    " ,  d.block)
        format += indent + "}"
    }
    fn formatIf(indent string ,s ast.StatementIf)-> (format string = "") {
        format = indent
        format += "if "
        for v := range s.prefixExpressions {
            format += this.formatExpression(indent , v)
            format += " ; "
        }
        format += this.formatExpression(indent , s.condition)
        format += " {\n"
        format += this.formatBlock(indent + "    " , s.block )
        if len(s.elseIfList) == 0 && s.Else == null {
            format += indent + "}\n"
        }else {
            format += indent + "}"
        }
        for k,v := range s.elseIfList{
            format += " else if " + this.formatExpression(indent , v.condition) + " {\n"
            format += this.formatBlock(indent + "    " , v.block )
            if k == len(s.elseIfList) -1 && s.Else == null {
                format += indent + "}\n"
            }else {
                format += indent + "}"
            }
        }
        if s.Else != null {
            format += " else {\n"
            format += this.formatBlock(indent + "    " , s.Else)
            format += indent + "}\n"
        }
    }
    fn formatGoto(indent string , s ast.StatementGoTo)-> (format string = "") {
        format = indent
        format += "goto " + s.labelName
    }
    fn formatFor(indent string ,s ast.StatementFor)-> (format string = "") {
        format = indent
        format += "for"
        if s.Init != null {
            format += " " + this.formatExpression(indent , s.Init)
        }
        if s.condition != null {
            if s.Init != null {
                format += ";"
            }
            format += " " + this.formatExpression(indent , s.condition)
        }
        if s.increment != null {
            format += " " + this.formatExpression(indent , s.increment)
        }
    }
    
    fn formatSwitch(indent string ,s ast.StatementSwitch)-> (format string = "") {
        format = indent
        format += "switch "
        for v := range s.prefixExpressions {
            format += this.formatExpression(indent , v)
            format += " ; "
        }
        if len(s.prefixExpressions) > 0 {
            format += " ; "
        }
        format += this.formatExpression(indent , s.condition)
        format += " {"
        for c := range s.StatementSwitchCases {
            format += indent + "    case"
            for k,e := range c.matches {
                format += this.formatExpression(indent ,e)
                if k != len(c.matches) -1 {
                    format += " , "
                }
            }
            format += ":\n"
            format += this.formatBlock("       " , c.block)
        }
        if s.Default != null {
            format += indent + "    default:\n" 
            format += this.formatBlock("       " , s.Default)
        }
        format += indent + "}"
    }

    fn formatWhen(indent string ,w ast.StatementWhen)-> (format string = "") {
        format = indent
        format += "when"
    }
    

    fn formatReturn(indent string ,s ast.StatementReturn)-> (format string = "") {
        format = indent 
        format += "return "
        for k,v := range s.expressions{
            format += this.formatExpression(indent , v)
            if k != len(s.expressions) - 1 {
                format += " , "
            }
        }
        format += "\n"
    }

    fn formatExpression(indent string ,e ast.Expression)-> (format string = "") {
        switch e.Type {
            case ast.ExpressionTypeNull:
                format = "null"
            case ast.ExpressionTypeBool:
                return "" + e.boolValue
            case ast.ExpressionTypeByte, ast.ExpressionTypeShort,ast.ExpressionTypeChar,
                ast.ExpressionTypeInt,
                ast.ExpressionTypeLong, ast.ExpressionTypeFloat , ast.ExpressionTypeDouble, ast.ExpressionTypeString:
                return e.literalRaw 
            case ast.ExpressionTypeArray:
                array := e.data.(ast.ExpressionArray)
                if array.Type == null {
                    format = "["
                    for k,v := range array.expressions{
                        format += this.formatExpression(indent , v)
                        if k != len(array.expressions) -1 {
                            format += " , "
                        }
                    }
                    format += "]"
                }else {
                    format =  array.Type.typeName() + "{"
                    for k,v := range array.expressions{
                        format += this.formatExpression(indent + "    " , v)
                        if k != len(array.expressions) -1 {
                            format += " , "
                        }
                    }
                    format += "}"
                }
            case ast.ExpressionTypeLogicalOr,ast.ExpressionTypeLogicalAnd,  ast.ExpressionTypeOr    ,ast.ExpressionTypeAnd ,
                ast.ExpressionTypeXor, ast.ExpressionTypeLsh , ast.ExpressionTypeRsh ,ast.ExpressionTypeAdd, 
                ast.ExpressionTypeSub , ast.ExpressionTypeMul  ,ast.ExpressionTypeDiv ,ast.ExpressionTypeMod:
                bin := e.data.(ast.ExpressionBinary)
                format = this.formatExpression(indent , bin.left) + " "+ 
                    e.op + " " + this.formatExpression(indent , bin.right)
            case ast.ExpressionTypeVarAssign:

            case ast.ExpressionTypeAssign, ast.ExpressionTypePlusAssign ,
                ast.ExpressionTypeMinusAssign ,ast.ExpressionTypeMulAssign ,ast.ExpressionTypeDivAssign ,
                ast.ExpressionTypeModAssign,  ast.ExpressionTypeAndAssign ,ast.ExpressionTypeOrAssign  ,
                ast.ExpressionTypeXorAssign ,ast.ExpressionTypeLshAssign , ast.ExpressionTypeRshAssign ,
                ast.ExpressionTypeEq , ast.ExpressionTypeNe ,   ast.ExpressionTypeGe,
                ast.ExpressionTypeGt  , ast.ExpressionTypeLe  ,   ast.ExpressionTypeLt:
                bin := e.data.(ast.ExpressionBinary) 
                
                format = this.formatExpression(indent , bin.left ) + " " + e.op + " " + this.formatExpression(indent , bin.right )
            case ast.ExpressionTypeIndex:   
                index := e.data.(ast.ExpressionIndex)
                format += this.formatExpression(indent , index.Expression)+"["
                format += this.formatExpression(indent , index.index)
                format += "]"
            case ast.ExpressionTypeSelection : 
                selection := e.data.(ast.ExpressionSelection)
                if selection.Expression.Type == ast.ExpressionTypeDot {
                    format += "." + selection.name    
                }else {
                    format += this.formatExpression(indent , selection.Expression)+"." + 
                    selection.name    
                }                               
            case ast.ExpressionTypeSelectionConst: 
                selection := e.data.(ast.ExpressionSelection)
                format += this.formatExpression(indent , selection.Expression)+"::" + 
                selection.name     
            case ast.ExpressionTypeMethodCall : 
                call := e.data.(ast.ExpressionMethodCall)
                if call.Expression.Type == ast.ExpressionTypeDot {
                    format += "." + call.name
                }else {
                    format += this.formatExpression(indent ,call.Expression) + "." + call.name + " ("
                }
                for k,v := range call.args {
                    format += this.formatExpression(indent ,v)
                    if k != len(call.args) - 1 {
                        format += " , "
                    }
                }
                format += " )"                           
            case ast.ExpressionTypeFunctionCall:  
                call := e.data.(ast.ExpressionFunctionCall)
                format += this.formatExpression(indent , call.Expression) + " ("
                for k,v := range call.args {
                    format += this.formatExpression( indent, v)
                    if k != len(call.args) - 1 {
                        format += " , "
                    }
                }
                format += ")"                                
            case ast.ExpressionTypeincrement: 
                format += this.formatExpression(indent , e.data.(ast.Expression)) + "++"                            
            case ast.ExpressionTypeDecrement:                                 
                format += this.formatExpression(indent , e.data.(ast.Expression)) + "--"    
            case ast.ExpressionTypePrefixincrement :
                format += "++" + this.formatExpression(indent, e.data.(ast.Expression))                           
            case ast.ExpressionTypePrefixDecrement:  
                format += "--" + this.formatExpression(indent , e.data.(ast.Expression))                     
            case ast.ExpressionTypeNegative ,ast.ExpressionTypeNot,ast.ExpressionTypeBitwiseNot:
                format += e.op + this.formatExpression(indent , e.data.(ast.Expression))                             
            case ast.ExpressionTypeIdentifier:
                format = e.data.(ast.ExpressionIdentifier).name
            case ast.ExpressionTypeNew: 
                n := e.data.(ast.ExpressionNew)
                format += "new " + n.Type.typeName()+ " ("
                for k,v := range n.args {
                    format += this.formatExpression(indent , v)
                    if k != len(n.args) - 1 {
                        format += " , "
                    }
                }
                format += ")"
            case ast.ExpressionTypeList: 
                list := e.data.([]ast.Expression)  
                for k,v := range list {
                    format += this.formatExpression(indent , v )
                    if k != len(list) - 1 {
                        format += " , "
                    }
                }                              
            case ast.ExpressionTypeFunctionLiteral:
                this.formatFunction(indent,e.data.(ast.Function),false )
            case ast.ExpressionTypeVar:  
                vs := e.data.(ast.ExpressionVar)
                format += "var "
                for k,v := range vs.variables {
                    format += v.name 
                    if k != len(vs.variables) -1 {
                        format += " , "
                    }
                }  
                if vs.Type != null {
                    format += " " + vs.Type.typeName()
                }
                if len(vs.InitValues) > 0 {
                    format += " = "
                    for k,v := range vs.InitValues {
                        format += this.formatExpression(indent , v ) 
                        if k != len(vs.InitValues) -1 {
                            format += " , "
                        }
                    }  
                }                            
            case ast.ExpressionTypeConst: 
                cs := e.data.([]ast.Constant)
                for v := range cs {
                    t := indent + "cosnt " + v.name + " "
                    if v.Type != null {
                        t += v.Type.typeName() + " "
                    }
                    t += "="
                    t += this.formatExpression(indent , v.defaultValueExpression) + "\n"
                    format += t 
                }                                 
            case ast.ExpressionTypeCheckCast:
                c := e.data.(ast.ExpressionTypeConversion) 
                format += c.Type.typeName() + "("
                format += this.formatExpression(indent , c.Expression)
                format += ")"                         
            case ast.ExpressionTypeRange:
                format =   "range " + this.formatExpression(indent , e.data.(ast.Expression))
            case ast.ExpressionTypeSlice: 
                s := e.data.(ast.ExpressionSlice)
                format += this.formatExpression(indent , s.ExpressionOn ) + "["
                if s.start != null {
                    format += this.formatExpression(indent , s.start ) 
                }
                format += " : "
                if s.end != null {
                    format += this.formatExpression(indent , s.end ) 
                }
                format += "]"                                
            case ast.ExpressionTypeMap:    
                m := e.data.(ast.ExpressionMap)
                if m.Type == null {
                    format += "{"
                    for k,v := range m.keyValuePairs {
                        format += this.formatExpression(indent , v.key) + " -> " 
                        format += this.formatExpression(indent , v.value)
                        if k != len(m.keyValuePairs) - 1 {
                            format += " , "
                        } 
                    }
                    format += "}"
                }else {
                    format += sprintf("map{%s -> %s}{" , m.Type.Map.K.typeName() ,  m.Type.Map.V.typeName())
                    for k,v := range m.keyValuePairs{
                        format += this.formatExpression(indent , v.key) + " -> " 
                        format += this.formatExpression(indent , v.value)
                        if k != len(m.keyValuePairs) - 1 {
                            format += " , "
                        } 
                    }
                    format += "}"
                }                             
            case ast.ExpressionTypeTypeAssert:
                a := e.data.(ast.ExpressionTypeAssert)   
                format += this.formatExpression(indent , a.Expression ) + ".("  
                format += a.Type.typeName()
                format += ")"                        
            case ast.ExpressionTypeQuestion: 
                q := e.data.(ast.ExpressionQuestion)
                format += this.formatExpression(indent , q.selection)  
                format += "?"
                format += this.formatExpression(indent , q.True) 
                format += ":"
                format += this.formatExpression(indent, q.False)                              
            case ast.ExpressionTypeGlobal:                  
                return "global"               
            case ast.ExpressionTypeParenthesis : 
                return "(" + this.formatExpression(indent , e.data.(ast.Expression)) + ")"                                
            case ast.ExpressionTypeVArgs:   
                return   this.formatExpression(indent , e.data.(ast.Expression)) + "..."                            
            case ast.ExpressionTypeDot:   
                return "."
            default:
                panic("not possible")                           
        }
    }
}

