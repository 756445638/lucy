// all rights reserved , check the LICENSE file

import "lucy/binary"

public class AttributeTemplateFunction {
    public name string
    /*
		reGenerate pos
	*/
    public filename    string
    public startLine   char  
    public startColumn char  
    public code        string
    public accessFlags char  

    public fn fromBytes(c Class , bs []byte) {
        this.name = string(c.constPools[binary.BigEndian.getChar(bs)].info)
        this.filename = string(c.constPools[binary.BigEndian.getChar(bs[2:])].info)
        this.startLine = binary.BigEndian.getChar(bs[4:])
        this.startColumn = binary.BigEndian.getChar(bs[6:])
        this.code = string(c.constPools[binary.BigEndian.getChar(bs[8:])].info)
        this.accessFlags = binary.BigEndian.getChar(bs[10:])
    }

    fn toAttributeInfo(c Class) -> (ret AttributeInfo) {
        ret = new AttributeInfo()
        ret.nameIndex = c.insertUtf8Const(AttributeNameLucyTemplateFunction)
        ret.attributeLength = 12
        ret.info = new []byte(ret.attributeLength)
        binary.BigEndian.putChar(ret.info , c.insertUtf8Const(this.name))
        binary.BigEndian.putChar(ret.info[2:] , c.insertUtf8Const(this.filename))
        binary.BigEndian.putChar(ret.info[4:] , this.startLine)
        binary.BigEndian.putChar(ret.info[6:] , this.startColumn)
        binary.BigEndian.putChar(ret.info[8:] , c.insertUtf8Const(this.code))
        binary.BigEndian.putChar(ret.info[10:] , this.accessFlags)
        return ret
    }
}


